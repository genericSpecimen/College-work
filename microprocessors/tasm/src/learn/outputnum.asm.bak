.model small
.stack 100h

.data
	num1 dw 1234
	buffstr db 10 dup('$')
	crlf	db 13, 10, '$'
.code

printstr proc
	push ax		; save current value of ax since ah gets modified

	mov ah, 09h
	; needs offset of string in dx
	int 21h

	pop ax		; restore previous value of ax
	ret
printstr endp

itos proc
	; converts value of ax to a string
	; algo:
	;	1. push each digit (starting from LSD) onto stack by dividing by 10 and pushing the remainder
	;	2. pop each digit, add 30h, and store it in string

	; -------- saving register values -------
	push bx
	push cx
	push dx
	; ---------------------------------------

	mov cx, 0	; keep track of number of digits pushed onto stack, used during pop cycle
	pushi:	; push each digit onto stack
		mov dx, 0	; required for division
		cmp ax, 0	; if ax is zero, then we are done pushing
		je retrieve

		mov bx, 10	; divide by 10
		div bx

		push dx		; remainder is in dx
		inc cx
		jmp pushi

	retrieve:
		mov si, offset buffstr	; output string is to be stored in buffstr
	popi:	; pop each digit and store it in buffstr
		cmp cx, 0	; if no digits pushed
		je writezero	; then write 0, and exit

		pop dx		; pop into dx
		add dl, 30h	; convert to ASCII
		mov [si], dl	; write at string index si
		inc si
		loop popi	; loop until cx == 0

	jmp exititos	; don't write zero if we are here

	writezero:
		mov dl, 30h
		mov [si], dl

	exititos:
	
	; ------- restore register values --------
	pop dx
	pop cx
	pop bx
	; -----------------------------------------
	ret
itos endp

main proc
	mov ax, @data
	mov ds, ax

	mov ax, num1
	call itos	; converts ax to string which is stored in buffstr

	mov dx, offset buffstr
	call printstr	

	mov dx, offset crlf
	call printstr	

	mov ax, 4c00h
	int 21h
main endp
end main

